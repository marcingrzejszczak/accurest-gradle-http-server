buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE"
		classpath "io.codearte.accurest:accurest-gradle-plugin:${accurestVersion}"
	}
}

ext {
	restAssuredVersion = '2.9.0'
	spockVersion = '1.0-groovy-2.4'
	wiremockVersion = '2.0.10-beta'

	accurestStubsBaseDirectory = 'src/test/resources/stubs'
}

group = 'io.codearte.accurest.testprojects'
version = '0.0.1-SNAPSHOT'

apply plugin: 'groovy'

repositories {
	mavenCentral()
	mavenLocal()
}

apply plugin: 'spring-boot'
apply plugin: 'accurest'
apply plugin: 'maven-publish'

ext {
	contractsDir = file("src/test/accurest")
	stubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
	wireMockStubsOutputDir = file(new File(stubsOutputDirRoot, 'mappings/'))
	contractsOutputDir = file(new File(stubsOutputDirRoot, 'accurest/'))
}

accurest {
	targetFramework = 'Spock'
	testMode = 'MockMvc'
	baseClassForTests = 'com.toomuchcoding.MvcSpec'
	contractsDslDir = contractsDir
	generatedTestSourcesDir = file("${project.buildDir}/generated-test-sources/")
	stubsOutputDir = wireMockStubsOutputDir
}

dependencies {
	compile("org.springframework.boot:spring-boot-starter-web") {
		exclude module: "spring-boot-starter-tomcat"
	}
	compile("org.springframework.boot:spring-boot-starter-jetty")
	compile("org.springframework.boot:spring-boot-starter-actuator")

	testRuntime "org.spockframework:spock-spring:$spockVersion"
	testCompile "org.springframework:spring-test"
	testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
	testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
	testCompile "org.codehaus.groovy:groovy-all:2.4.5"
	testCompile "org.spockframework:spock-core:$spockVersion"
	testCompile "junit:junit:4.12"
	testCompile "com.github.tomakehurst:wiremock:$wiremockVersion"
}

task stubsJar(type: Jar, dependsOn: "generateWireMockClientStubs") {
	baseName = "${project.name}"
	classifier = "stubs"
	from stubsOutputDirRoot
}

artifacts {
	archives stubsJar
}

publishing {
	publications {
		stubs(MavenPublication) {
			artifactId "${project.name}-stubs"
			artifact stubsJar
		}
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
